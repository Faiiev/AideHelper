apply plugin: 'com.android.application'

android {
    signingConfigs {
    }
    compileSdkVersion 28
    defaultConfig {
        applicationId "me.tvcfish.xposed.aidehelper"
        minSdkVersion 15
        targetSdkVersion 28
        versionCode 11000
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'androidx.annotation:annotation:1.0.1'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.1'
    //捐赠
    implementation 'com.canking.minipay:minipay:1.0.2'
    //网络
    implementation 'com.squareup.okhttp3:okhttp:3.12.1'
    //数据库
    implementation 'org.litepal.android:java:3.0.0'
    //xposed
    compileOnly 'de.robv.android.xposed:api:82'
}

afterEvaluate {
    installDebug.doLast {
        updateVirtualXposedAPP.execute()
        rebootVirtualXposedAPP.execute()
        launchVirtualXposedAPP.execute()
    }
}
// 更新 VXP 中的 app
task updateVirtualXposedAPP(type: Exec) {
    def pkg = android.defaultConfig.applicationId
    commandLine android.adbExecutable, 'shell', 'am', 'broadcast', '-a', 'io.va.exposed.CMD', '-e', 'cmd', 'update', '-e', 'pkg', pkg
}
// 重启 VXP
task rebootVirtualXposedAPP(type: Exec) {
    commandLine android.adbExecutable, 'shell', 'am', 'broadcast', '-a', 'io.va.exposed.CMD', '-e', 'cmd', 'reboot'
}
// 重启 VXP 需要 hook 的 APP，需要知道它的包名
task launchVirtualXposedAPP(type: Exec) {
    def pkg = 'me.tvcfish.xposed.aidehelper'// 需要 hook 的 app
    commandLine android.adbExecutable, 'shell', 'am', 'broadcast', '-a', 'io.va.exposed.CMD', '-e', 'cmd', 'launch', '-e', 'pkg', pkg
}